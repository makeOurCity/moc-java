plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group = 'city.makeour'
version = '1.0.0'
sourceCompatibility = '17'
targetCompatibility = '17'

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.10.1'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.3.1'
    
    implementation 'software.amazon.awssdk:cognitoidentityprovider:2.31.20'
    implementation 'com.github.makeOurCity:ngsiv2-java:0.0.2'
    implementation 'org.bouncycastle:bcprov-jdk15on:1.70'
    implementation 'org.springframework:spring-webmvc:5.3.23'
}

java {
    withJavadocJar()
    withSourcesJar()
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            
            pom {
                name = 'MoC Java Client'
                description = 'MakeOurCity (MoC) client library for Java'
                url = 'https://github.com/makeOurCity/moc-java'
                
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                
                developers {
                    developer {
                        name = 'MakeOurCity Team'
                        email = 'info@makeour.city'
                        organization = 'MakeOurCity'
                        organizationUrl = 'https://makeour.city'
                    }
                }
                
                scm {
                    connection = 'scm:git:git://github.com/makeOurCity/moc-java.git'
                    developerConnection = 'scm:git:ssh://github.com:makeOurCity/moc-java.git'
                    url = 'https://github.com/makeOurCity/moc-java'
                }
            }
        }
    }
    
    repositories {
        maven {
            name = "OSSRH"
            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            
            credentials {
                username = System.getenv("OSSRH_USERNAME") ?: project.findProperty("ossrhUsername")
                password = System.getenv("OSSRH_PASSWORD") ?: project.findProperty("ossrhPassword")
            }
        }
    }
}

signing {
    required { !version.endsWith('SNAPSHOT') && gradle.taskGraph.hasTask("publish") }
    sign publishing.publications.mavenJava
}
